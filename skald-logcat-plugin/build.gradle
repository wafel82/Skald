apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

// ===============================================================================================//
// VERSIONING
// ===============================================================================================//
ext.versionMajor = 0
ext.versionMinor = 4
ext.versionPatch = 0
ext.versionClassifier = ""
ext.minimumSdkVersion = 9


// ===============================================================================================//
// MAVEN DEPLOY CONFIGURATION
// ===============================================================================================//
group = 'com.wojtek.wawerek'
ext {
    bintrayRepo = 'android'
    publishedGroupId = 'com.wojtek.wawerek'
    bintrayName = 'skald-logcat-plugin'
    artifact = 'skald-logcat-plugin'
    libraryVersion = generateVersionName()

    libraryDescription = 'Logcat Appender plugin for Skald logging framework'
    siteUrl = 'https://github.com/wafel82/Skald'
    gitUrl = 'git@github.com:wafel82/Skald.git'
    licenseName = 'The MIT License (MIT)'
    allLicenses = ["MIT"]
}

// ===============================================================================================//
// ANDROID
// ===============================================================================================//
android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 26
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'

    compileOnly project(':skald-kotlin')
}

// ===============================================================================================//
// HELPERS
// ===============================================================================================//
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName = versionName + "-" + ext.versionClassifier
    }
    return versionName
}


apply from: '../bintray-upload.gradle'